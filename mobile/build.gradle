buildscript {
  repositories {
    jcenter()

    maven {
      url 'https://maven.google.com'
    }
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.0.0-alpha4'
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.3'
    classpath 'com.google.gms:google-services:3.0.0'

    classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: 'jacoco-android'

apply from: '../tools/script-dependencies.gradle'
apply from: '../tools/script-git-version.gradle'

android {
  compileSdkVersion configuration.compileSdkVersion
  buildToolsVersion configuration.buildToolsVersion

  buildTypes {
    debug {
      minifyEnabled false
      multiDexEnabled true
    }
  }

  defaultConfig {
    applicationId 'io.ashdavies.cinnamon'

    minSdkVersion configuration.minSdkVersion
    targetSdkVersion configuration.targetSdkVersion

    versionCode configuration.versionCode
    versionName configuration.versionName
  }

  compileOptions {
    sourceCompatibility configuration.sourceCompatibility
    targetCompatibility configuration.targetCompatibility
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
}

kapt {
  correctErrorTypes = true
}

dependencies {

  // Kotlin
  compile libraries.kotlin

  // Support
  compile libraries.android.support.compat
  compile libraries.android.support.cards
  compile libraries.android.support.design
  compile libraries.android.support.recycler

  // Architecture
  compile libraries.android.arch.lifecycle.runtime
  compile libraries.android.arch.lifecycle.extensions
  kapt libraries.android.arch.lifecycle.compiler

  // Google Play
  compile libraries.google.play.services
  compile libraries.google.play.vision

  // ButterKnife
  compile libraries.butterknife.core
  kapt libraries.butterknife.compiler

  // Dagger
  compile libraries.dagger.core
  compile libraries.dagger.android.support
  kapt libraries.dagger.compiler
  kapt libraries.dagger.android.processor

  // Firebase
  compile libraries.firebase.core
  compile libraries.firebase.auth
  compile libraries.firebase.database
  compile libraries.firebase.config
  compile libraries.firebase.crash
  compile libraries.firebase.analytics

  // Network
  compile libraries.gson
  compile libraries.retrofit.core
  compile libraries.retrofit.adapter
  compile libraries.retrofit.converter
  compile libraries.okhttp.core
  compile libraries.okhttp.logging

  // RxJava
  compile libraries.rx.java
  compile libraries.rx.android
  compile libraries.rx.firebase

  // Timber
  compile libraries.timber

  // Barcode
  compile libraries.scanner

  // Conceal
  compile libraries.conceal

  // AutoNoOp
  compile libraries.auto.noOp.core
  kapt libraries.auto.noOp.compiler

  // AutoValue
  compile libraries.auto.value.core
  kapt libraries.auto.value.compiler

  // AutoValue: Firebase
  compile libraries.auto.firebase.adapter
  provided libraries.auto.firebase.core
  kapt libraries.auto.firebase.compiler

  // Testing
  testCompile libraries.jUnit
  testCompile libraries.truth
  testCompile libraries.mockito
}

apply plugin: 'com.google.gms.google-services'
